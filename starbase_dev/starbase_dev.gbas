// --------------------------------- //
// Project: starbase_dev
// Start: Monday, December 08, 2014
// IDE Version: 12.312


// SETCURRENTDIR("Media") // go to media files
TYPE optionsType
	debugmodus
	developermodus

	loglevel_info
	loglevel_warning
	loglevel_critical
	loglevel_error
ENDTYPE

GLOBAL options AS optionsType

main()

FUNCTION main:

	init(2)

	WHILE TRUE

		SHOWSCREEN
	WEND
ENDFUNCTION

FUNCTION init: id

	AUTOPAUSE FALSE
	SYSTEMPOINTER TRUE
	checkCommandline()
	
	SELECT id
	CASE 1
		options.debugmodus = TRUE
	CASE 2
		options.developermodus = TRUE
	ENDSELECT
	checkLogfile()
	SETSCREEN checkOptions(0), checkOptions(1), checkOptions(2)

ENDFUNCTION

FUNCTION checkCommandline:
LOCAL debuginfo$, count, spl$[]
	debuginfo$ = GETCOMMANDLINE$()

	count = SPLITSTR(debuginfo$, spl$[], " -")

	IF count = 2
		IF spl$[1] = "debug" THEN options.debugmodus = TRUE
		IF spl$[1] = "developermodus:123" THEN options.developermodus = TRUE
	ENDIF

ENDFUNCTION

FUNCTION checkLogfile:
LOCAL ok

	IF options.debugmodus = TRUE OR options.developermodus = TRUE
		ok = DOESDIREXIST("log")
		IF ok = FALSE THEN CREATEDIR("log")

		ok = DOESFILEEXIST("log/log.ini")
		IF ok = FALSE
			INIOPEN "log/log.ini"
				IF options.debugmodus = TRUE
					INIPUT "logging", "info", "0"
					INIPUT "logging", "warning", "0"
					INIPUT "logging", "critical", "0"
					INIPUT "logging", "error", "1"
				ENDIF
				IF options.developermodus = TRUE
					INIPUT "logging", "info", "0"
					INIPUT "logging", "warning", "1"
					INIPUT "logging", "critical", "1"
					INIPUT "logging", "error", "1"
				ENDIF
			INIOPEN ""

			IF options.debugmodus = TRUE
				options.loglevel_info = 0
				options.loglevel_warning = 0
				options.loglevel_critical = 0
				options.loglevel_error = 1
			ENDIF

			IF options.developermodus = TRUE
				options.loglevel_info = 0
				options.loglevel_warning = 1
				options.loglevel_critical = 1
				options.loglevel_error = 1
			ENDIF
		ENDIF

		IF ok = TRUE
			INIOPEN "log/log.ini"
				options.loglevel_info = INIGET$("logging", "info")
				options.loglevel_warning = INIGET$("logging", "warning")
				options.loglevel_critical = INIGET$("logging", "critical")
				options.loglevel_error = INIGET$("logging", "error")
			INIOPEN ""
		ENDIF
	ENDIF

	DEBUG options.loglevel_info + "|" + options.loglevel_warning + "|" + options.loglevel_critical + "|" + options.loglevel_error

ENDFUNCTION

FUNCTION writeLog: info$, loglevel
LOCAL date$, ok, entry$, writeTextInLog, level$

	writeTextInLog = FALSE

	date$ = MID$(PLATFORMINFO$("TIME"), 0, 10)
	//DEBUG date$ + "\n"

	ok = DOESFILEEXIST("log/log" + date$ + ".txt")

	IF ok = FALSE
		OPENFILE(0, "log/log" + date$ + ".txt", -1)
			WRITELINE 0, date$
		CLOSEFILE 0
	ENDIF
	
	SELECT loglevel
	CASE 0
		IF options.loglevel_info = 1 THEN writeTextInLog = TRUE
		level$ = "INFO"
	CASE 1
		IF options.loglevel_warning = 1 THEN writeTextInLog = TRUE
		level$ = "WARNING"
	CASE 2
		IF options.loglevel_critical = 1 THEN writeTextInLog = TRUE
		level$ = "CRITICAL"
	CASE 3
		IF options.loglevel_error = 1 THEN writeTextInLog = TRUE
		level$ = "ERROR"
	ENDSELECT
	
	IF ok = TRUE AND writeTextInLog = TRUE
		OPENFILE(0, "log/log" + date$ + ".txt", -1)
			entry$ = PLATFORMINFO$("TIME") + "|> " + level$ + ": " + info$
			WRITELINE 0, entry$
		CLOSEFILE 0
	ENDIF



ENDFUNCTION

FUNCTION checkOptions: id
LOCAL error
STATIC ok, found
STATIC resX, resY, full

	ok = DOESFILEEXIST("options.ini")

	// Initialisierung wird erzeugt
	IF found  = FALSE

		IF ok = TRUE
			writeLog("options.ini found.", 0)
			
			resX = optionsFound(0)
			resY = optionsFound(1)
			full = optionsFound(2)

			IF resX = -1 OR resY = -1 OR full = -1
				KILLFILE "options.ini"
				optionsNotFound()
				resX = 1024
				resY = 768
				full = 0
				
				writeLog("options.ini set with wrong settings. File deleted and created with default settings.", 2)			
			ENDIF

		ENDIF
		IF ok = FALSE
			optionsNotFound()
			resX = 1024
			resY = 768
			full = 0
			
			writeLog("options.ini not found. File created.", 1)
		ENDIF

		found = TRUE
	ENDIF

	// Initialisierung durchgeführt
	IF id = 0 THEN RETURN resX
	IF id = 1 THEN RETURN resY
	IF id = 2 THEN RETURN full

ENDFUNCTION

FUNCTION optionsFound: id
LOCAL info$, info, error$

	INIOPEN "options.ini"
	
		IF id = 0 THEN info$ = INIGET$("options", "resX")
		IF id = 1 THEN info$ = INIGET$("options", "resY")
		IF id = 2 THEN info$ = INIGET$("options", "full")

		writeLog("read " + info$ + " from options.ini", 0)

		TRY
			info = info$
			IF MOD(info, 8) <> 0 AND id < 2 THEN THROW "not8"
		CATCH error$
			IF error$ = "not8" THEN info$ = "-1"
			
			writeLog("not possible to divide value by 8: "+ info$, 2)
		FINALLY

		RETURN info$
	INIOPEN ""

ENDFUNCTION

FUNCTION optionsNotFound:

	INIOPEN "options.ini"
		INIPUT "options", "resX", "1024"
		INIPUT "options", "resY", "768"
		INIPUT "options", "full", "0"
	INIOPEN ""

ENDFUNCTION